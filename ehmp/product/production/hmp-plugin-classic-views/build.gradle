buildscript {
    repositories {
		maven {
	        url "http://hmpcode.vainnovations.us:8080/nexus/content/groups/public"
	        credentials {
	            username nexusUser
	            password nexusPassword
	        }
	    }
	 	mavenCentral()
    }
    dependencies {
 		classpath 'com.eriwen:gradle-js-plugin:1.11.0'
        classpath 'be.jlr-home.gradle:scrPlugin:0.1.+'
    }
}

apply plugin: 'java'
apply plugin: 'js'
apply plugin: 'osgi'
apply plugin: 'maven'
apply plugin: 'scr'

group = 'gov.va.cpe.cprs'
version = '1.0-SNAPSHOT'

description = "CPE Classic Views"

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext {
    hmpApiVersion = '0.7-S59'
    osgiVersion = '5.0.0'
}

repositories {
    mavenLocal()
    maven {
        url "http://hmpcode.vainnovations.us:8080/nexus/content/groups/public"
        credentials {
            username nexusUser
            password nexusPassword
        }
    }
}

dependencies {
    compile group: 'gov.va.hmp', name: 'hmp-api', version: hmpApiVersion
    compile group: 'org.apache.felix', name: 'org.apache.felix.http.api', version:'2.2.2'
    compile(group: 'org.osgi', name: 'org.osgi.core', version: osgiVersion)
    compile(group: 'org.osgi', name: 'org.osgi.enterprise', version: osgiVersion)
    compile group: 'org.apache.felix', name: 'org.apache.felix.scr.annotations', version:'1.9.6'
    compile group: 'org.apache.felix', name: 'org.apache.felix.scr.ds-annotations', version:'1.2.4'
}

javascript.source {
    dev {
        js {
            srcDir 'src/main/js'
            include '**/*.js'
            exclude '*.min.js'
        }
    }
}

combineJs {
    source = javascript.source.dev.js.files
    dest = file("${buildDir}/all.js")
}
processResources.dependsOn(combineJs)

task copyJs(type: Copy) {

}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task wrapper(type: Wrapper) {
    description = "Generates the wrapper files to enable 'gradlew'"
    gradleVersion = '1.12'
}

jar {
    manifest { // the manifest of the default jar is of type OsgiManifest
        instruction 'Include-Resource','/js=src/main/js'
        instruction 'Bundle-Vendor', 'Department of Veterans Affairs - Health Informatics Initiative'
        instruction 'Bundle-Description', 'Modules for the HMP in the style of CPRS'
        instruction 'Bundle-DocURL', 'https://vahmp.vainnovations.us:8080'
    }
    from("src/main/js") {
        into("js")
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://hmpcode.vainnovations.us:8080/nexus/content/repositories/releases/") {
                authentication(userName: nexusUser, password: nexusPassword)
            }
            snapshotRepository(url: "http://hmpcode.vainnovations.us:8080/nexus/content/repositories/snapshots/") {
                authentication(userName: nexusUser, password: nexusPassword)
            }
        }
    }
}
